#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(binding = 0) uniform samplerCube u_environment;
layout(binding = 1, rgba32f) writeonly uniform imageCube u_irradiance;

layout(push_constant) uniform PC {
  uint face;
  uint resolution;
  uint sampleCount;
  uint envResolution;
};

#define MATH_PI 3.1415926535897932384626433832795

// Vulkan cubemap face convention
vec3 uvToXYZ(int face, vec2 uv)
{
  if (face == 0) return vec3( 1.0, -uv.y, -uv.x);
  if (face == 1) return vec3(-1.0, -uv.y,  uv.x);
  if (face == 2) return vec3( uv.x,  1.0,  uv.y);
  if (face == 3) return vec3( uv.x, -1.0, -uv.y);
  if (face == 4) return vec3( uv.x, -uv.y,  1.0);
  return            vec3(-uv.x, -uv.y, -1.0);
}

float radicalInverse_VdC(uint bits)
{
  bits = (bits << 16u) | (bits >> 16u);
  bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
  bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
  bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
  bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
  return float(bits) * 2.3283064365386963e-10;
}

vec2 hammersley2d(uint i, uint N)
{
  return vec2(float(i) / float(N), radicalInverse_VdC(i));
}

mat3 generateTBN(vec3 normal)
{
  vec3 bitangent = vec3(0.0, 1.0, 0.0);

  float NdotUp = dot(normal, vec3(0.0, 1.0, 0.0));
  if (1.0 - abs(NdotUp) <= 1e-7)
  {
    bitangent = NdotUp > 0.0 ? vec3(0.0, 0.0, 1.0) : vec3(0.0, 0.0, -1.0);
  }

  vec3 tangent = normalize(cross(bitangent, normal));
  bitangent = cross(normal, tangent);

  return mat3(tangent, bitangent, normal);
}

float computeLod(float pdf, float width, float numSamples)
{
  return 0.5 * log2(6.0 * width * width / (numSamples * pdf));
}

void main()
{
  uvec2 pos = gl_GlobalInvocationID.xy;
  if (pos.x >= resolution || pos.y >= resolution)
    return;

  vec2 uv = (vec2(pos) + 0.5) / float(resolution) * 2.0 - 1.0;
  vec3 N = normalize(uvToXYZ(int(face), uv));

  mat3 TBN = generateTBN(N);

  vec3 color = vec3(0.0);

  for (uint i = 0; i < sampleCount; ++i)
  {
    vec2 xi = hammersley2d(i, sampleCount);

    // Cosine-weighted hemisphere sampling (Lambertian)
    float cosTheta = sqrt(1.0 - xi.y);
    float sinTheta = sqrt(xi.y);
    float phi = 2.0 * MATH_PI * xi.x;

    vec3 localDir = vec3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);
    vec3 H = TBN * localDir;

    // PDF for cosine-weighted sampling
    float pdf = cosTheta / MATH_PI;
    float lod = computeLod(pdf, float(envResolution), float(sampleCount));

    // The NdotH / pdf / PI terms cancel for Lambertian (Khronos reference)
    color += textureLod(u_environment, H, lod).rgb;
  }

  // Average and multiply by PI to store irradiance E = PI * avg(L)
  // The fragment shader divides by PI: getIBLDiffuseLight() = irradiance / PI
  color = color / float(sampleCount) * MATH_PI;

  imageStore(u_irradiance, ivec3(pos, int(face)), vec4(color, 1.0));
}
