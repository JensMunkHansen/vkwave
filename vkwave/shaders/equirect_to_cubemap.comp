#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(binding = 0) uniform sampler2D u_equirect;
layout(binding = 1, rgba32f) writeonly uniform imageCube u_cubemap;

layout(push_constant) uniform PC {
  uint face;
  uint resolution;
};

#define MATH_PI 3.1415926535897932384626433832795

// Vulkan cubemap face convention (matches hardware textureLod/imageStore mapping)
// Face 0 (+X): direction proportional to ( 1, -tc, -sc)
// Face 1 (-X): direction proportional to (-1, -tc,  sc)
// Face 2 (+Y): direction proportional to ( sc,  1,  tc)
// Face 3 (-Y): direction proportional to ( sc, -1, -tc)
// Face 4 (+Z): direction proportional to ( sc, -tc,  1)
// Face 5 (-Z): direction proportional to (-sc, -tc, -1)
// where sc = 2u-1, tc = 2v-1
vec3 uvToXYZ(int face, vec2 uv)
{
  if (face == 0) return vec3( 1.0, -uv.y, -uv.x);
  if (face == 1) return vec3(-1.0, -uv.y,  uv.x);
  if (face == 2) return vec3( uv.x,  1.0,  uv.y);
  if (face == 3) return vec3( uv.x, -1.0, -uv.y);
  if (face == 4) return vec3( uv.x, -uv.y,  1.0);
  return            vec3(-uv.x, -uv.y, -1.0);
}

// 3D direction to equirectangular UV (Vulkan: v=0 is top of image = north pole)
vec2 dirToUV(vec3 dir)
{
  return vec2(
    0.5 + 0.5 * atan(dir.z, dir.x) / MATH_PI,
    acos(dir.y) / MATH_PI);
}

void main()
{
  uvec2 pos = gl_GlobalInvocationID.xy;
  if (pos.x >= resolution || pos.y >= resolution)
    return;

  // Map pixel to UV in [-1, 1]
  vec2 uv = (vec2(pos) + 0.5) / float(resolution) * 2.0 - 1.0;

  vec3 dir = normalize(uvToXYZ(int(face), uv));
  vec2 src = dirToUV(dir);

  vec4 color = texture(u_equirect, src);

  imageStore(u_cubemap, ivec3(pos, int(face)), color);
}
